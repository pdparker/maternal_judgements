TITLE: Mediation Mod for Math: Free Cross Paths
DATA: FILE = "../../data/mplus_data.dat";
VARIABLE: 
NAMES = y3_weight y3_math_interest y3_math_judgement y5_math_interest
     y5_math_judgement y7_math_interest y7_math_judgement y3_math y5_math y7_math
     y3_math_sch y5_math_sch y7_math_sch y3_read_interest y3_read_judgement
     y5_read_interest y5_read_judgement y7_read_interest y7_read_judgement y3_read
     y5_read y7_read y3_read_sch y5_read_sch y7_read_sch gender ses geo indig lang
     y3_sid; 
MISSING=.;

USEVARIABLES ARE  y3_read_judgement y5_read_judgement y7_read_judgement
				  y3_read_interest y5_read_interest y7_read_interest
				  y3_read y5_read y7_read
				  gender ses geo indig lang;

WEIGHT ARE y3_weight;

CLUSTER y3_sid;

CATEGORICAL ARE 
	y5_read_judgement y7_read_judgement
	y5_read_interest y7_read_interest;

DEFINE:
STANDARDIZE y3_read y5_read y7_read ses;


ANALYSIS: ESTIMATOR = WLSMV; TYPE = COMPLEX; PARAMETERIZATION = THETA;

MODEL:
y7_read_judgement ON y5_read_judgement y5_read_interest y5_read;
y7_read_interest  ON y5_read_judgement y5_read_interest y5_read;
y7_read           ON y5_read_judgement y5_read_interest y5_read;

y5_read_judgement ON y3_read_judgement y3_read_interest y3_read;
y5_read_interest  ON y3_read_judgement y3_read_interest y3_read;
y5_read           ON y3_read_judgement y3_read_interest y3_read;

y7_read_judgement ON y3_read_judgement;
y7_read_interest  ON y3_read_interest;
y7_read           ON y3_read;

y7_read_judgement y7_read_interest y7_read WITH
	y7_read_judgement y7_read_interest y7_read;

y5_read_judgement y5_read_interest y5_read WITH
	y5_read_judgement y5_read_interest y5_read;

y3_read_judgement y3_read_interest y3_read WITH
	y3_read_judgement y3_read_interest y3_read;

y3_read_judgement-y7_read ON gender ses geo indig lang;


MODEL INDIRECT:
y5_read IND gender;
y7_read IND gender;


OUTPUT: stdyx; CINTERVAL; MODINDICES;